#version 460
//
// Copyright 2025 Hillbot Inc.
// Copyright 2020-2024 UCSD SU Lab
// 
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at:
// 
//     http://www.apache.org/licenses/LICENSE-2.0
// 
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.
//
#extension GL_EXT_ray_tracing : require
#extension GL_ARB_shader_clock : enable
#extension GL_GOOGLE_include_directive : enable


#include "random.glsl"
#include "ray.glsl"

layout(location = 0) rayPayloadEXT Ray ray;

layout(set = 0, binding = 0, rgba32f) uniform image2D outHdrColor;
layout(set = 0, binding = 1, rgba32f) uniform image2D outAlbedo;
layout(set = 0, binding = 2, rgba32f) uniform image2D outNormal;
layout(set = 0, binding = 3, rgba32ui) uniform uimage2D outSegmentation;
layout(set = 0, binding = 4, rgba32f) uniform image2D outRadiance;
layout(set = 0, binding = 5, rgba32f) uniform image2D outPosition;

layout(set = 1, binding = 0) uniform accelerationStructureEXT tlas;

layout(set = 2, binding = 0) uniform CameraBuffer {
  mat4 viewMatrix;
  mat4 projectionMatrix;
  mat4 viewMatrixInverse;
  mat4 projectionMatrixInverse;
  mat4 prevViewMatrix;
  mat4 prevViewMatrixInverse;
  float width;
  float height;
} cameraBuffer;

#include "push_constant.glsl"



void main() {
  vec2 invScreen = vec2(2.0) / gl_LaunchSizeEXT.xy;
  vec2 pixel = gl_LaunchIDEXT.xy * invScreen - 1.0;
  uint magic = 0x9e3779b9;  // use fixed seed for consistency
  // uint magic = uint(clockARB());
  uint seed = tea(gl_LaunchSizeEXT.x * gl_LaunchSizeEXT.y * spp * frameCount +
                  gl_LaunchIDEXT.y * gl_LaunchSizeEXT.x + gl_LaunchIDEXT.x, magic);

  vec3 radiance = vec3(0.0);
  float alpha = 0.0;

  uint rayFlags = gl_RayFlagsNoneEXT;
  float tMin = 0.001f;
  float tMax = 10000.0f;

  ray.seed = seed;

  float effectiveSpp = 0.0;
  for (int i = 0; i < spp; ++i) {
    // sample point on aperture
    float at = rnd(ray.seed) * 2.0 * M_PI;
    float ar = rnd(ray.seed) * aperture;
    float ax = ar * cos(at);
    float ay = ar * sin(at);

    // vec2 r = vec2(rnd(ray.seed), rnd(ray.seed));
    vec2 r = halton2d(frameCount * spp + i, 2, 3);

    vec2 d = pixel + r * invScreen;  // jittered pixel position
    vec4 csDir = cameraBuffer.projectionMatrixInverse * vec4(d, 1.0, 1.0);

    vec3 csFocusPoint = csDir.xyz / (-csDir.z) * focusPlane;
    vec3 csFocusOrigin = vec3(ax, ay, 0.0);
    vec3 csFocusDir = csFocusPoint - csFocusOrigin;

    // vec4 origin = cameraBuffer.viewMatrixInverse * vec4(0.0, 0.0, 0.0, 1.0);
    // vec4 direction = cameraBuffer.viewMatrixInverse * vec4(csDir.xyz, 0.0);
    vec4 origin = cameraBuffer.viewMatrixInverse * vec4(csFocusOrigin, 1.0);
    vec4 direction = cameraBuffer.viewMatrixInverse * vec4(csFocusDir, 0.0);

    ray.origin = origin.xyz;
    ray.direction = normalize(direction.xyz);
    ray.albedo = vec3(0.0);
    ray.normal = vec3(0.0);
    ray.radiance = vec3(0.0);
    ray.alpha = 1.0;
    ray.attenuation = vec3(1.0);
    ray.depth = 0;

    vec3 attenuation = vec3(1.0);
    for (ray.depth = 0; ray.depth <= maxDepth; ++ray.depth) {
      bool first = ray.depth == 0;

      traceRayEXT(tlas,
                  rayFlags,
                  0xff,
                  0,
                  0,
                  0,
                  ray.origin,
                  tMin,
                  ray.direction,
                  tMax,
                  0);

      radiance += ray.radiance * attenuation;
      attenuation *= ray.attenuation;

      if (first) {
        effectiveSpp += ray.alpha;
        alpha += ray.alpha;
      }

      if (first) {
        imageStore(outNormal, ivec2(gl_LaunchIDEXT.xy), vec4(mat3(cameraBuffer.viewMatrix) * ray.normal, 1.0));
        imageStore(outAlbedo, ivec2(gl_LaunchIDEXT.xy), vec4(ray.albedo, 1.0));
        imageStore(outSegmentation, ivec2(gl_LaunchIDEXT.xy), ray.segmentation);

        vec4 position = cameraBuffer.viewMatrix * vec4(ray.origin, 1.0);
        vec4 ndc = cameraBuffer.projectionMatrix * position;
        imageStore(outPosition, ivec2(gl_LaunchIDEXT.xy), vec4(position.xyz, ndc.z / ndc.w));
      }
    }
  }
  radiance /= effectiveSpp + 1e-6;
  alpha /= spp;

  // normal background
  if (frameCount > 0) {
    vec4 oldColor = imageLoad(outRadiance, ivec2(gl_LaunchIDEXT.xy));
    vec4 newColor = vec4(mix(oldColor, vec4(radiance, alpha), 1.0 / float(frameCount + 1)));
    imageStore(outRadiance, ivec2(gl_LaunchIDEXT.xy), newColor);
    imageStore(outHdrColor, ivec2(gl_LaunchIDEXT.xy), newColor);
  } else {
    imageStore(outRadiance, ivec2(gl_LaunchIDEXT.xy), vec4(radiance, alpha));
    imageStore(outHdrColor, ivec2(gl_LaunchIDEXT.xy), vec4(radiance, alpha));
  }

}